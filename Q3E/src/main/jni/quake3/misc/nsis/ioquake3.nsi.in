; NSIS description file for ioquake3 installer
;
; EDIT WITH CARE!
;
; This file is used to automatically build the installers in the
; openSUSE build service, don't break this!
;
; you have to copy OpenAL32.dll here manually

!define VERSION "XXXVERSIONXXX"

!define MULTIUSER_MUI
!define MULTIUSER_EXECUTIONLEVEL Highest
!define MULTIUSER_INSTALLMODE_COMMANDLINE
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY "Software\ioquake3"
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME "Install_Mode"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY "Software\ioquake3"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME "Install_Dir"
!define MULTIUSER_INSTALLMODE_INSTDIR "ioquake3"
!include MultiUser.nsh

!include "MUI2.nsh"
!define MUI_ICON "../quake3.ico"

!include LogicLib.nsh

; The name of the installer
Name "ioquake3"

; The file to write
OutFile "ioquake3-${VERSION}-XXXRELEASEXXX.x86.exe"

; The default installation directory
; set by Multiuser.nsh
;InstallDir $PROGRAMFILES\ioquake3

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
; handled by Multiuser.nsh
;InstallDirRegKey HKLM "Software\ioquake3" "Install_Dir"

;--------------------------------
;Interface Settings

!define MUI_ABORTWARNING

;--------------------------------
;Pages

!insertmacro MULTIUSER_PAGE_INSTALLMODE
;!insertmacro MUI_PAGE_LICENSE "../../COPYING.txt"
!define MUI_COMPONENTSPAGE_SMALLDESC
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English"

;--------------------------------

;--------------------------------
;Multiuser stuff
Function .onInit
  !insertmacro MULTIUSER_INIT
FunctionEnd

Function un.onInit
  !insertmacro MULTIUSER_UNINIT
FunctionEnd

; The stuff to install
Section "ioquake3 ${VERSION} (required)" ioquake3

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  File "../../build/release-mingw32-x86/SDL2.dll"
!ifndef USE_INTERNAL_ZLIB
  File "zlib1.dll"
!endif
!ifndef USE_INTERNAL_JPEG
  File "jpeg8c.dll"
!endif

  ; Put file there
  File "../../build/release-mingw32-x86/ioq3ded.x86.exe"
  File "../../build/release-mingw32-x86/ioquake3.x86.exe"
!ifdef USE_RENDERER_DLOPEN
  File "../../build/release-mingw32-x86/renderer_opengl1_x86.dll"
  File "../../build/release-mingw32-x86/renderer_opengl2_x86.dll"
!endif

  File "../../COPYING.txt"
  File "/oname=README.txt" "../../README.md"
  File "../../id-readme.txt"
  File "../../voip-readme.txt"

  SetOutPath "$INSTDIR\baseq3"
  File "../../build/release-mingw32-x86/baseq3/cgamex86.dll"
  File "../../build/release-mingw32-x86/baseq3/qagamex86.dll"
  File "../../build/release-mingw32-x86/baseq3/uix86.dll"
  SetOutPath "$INSTDIR\missionpack"
  File "../../build/release-mingw32-x86/missionpack/cgamex86.dll"
  File "../../build/release-mingw32-x86/missionpack/qagamex86.dll"
  File "../../build/release-mingw32-x86/missionpack/uix86.dll"

  ; Write the installation path into the registry
  WriteRegStr SHCTX "Software\ioquake3" ${MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME} "$INSTDIR"
  WriteRegStr SHCTX "Software\ioquake3" ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME} "$MultiUser.InstallMode"

  ; Write the uninstall keys for Windows
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3" "DisplayName" "ioquake3"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3" "DisplayVersion" "${VERSION}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3" "NoModify" 1
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts" StartMenuShortcuts

  CreateDirectory "$SMPROGRAMS\ioquake3"
  CreateShortCut "$SMPROGRAMS\ioquake3\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\ioquake3\ioquake3.lnk" "$INSTDIR\ioquake3.x86.exe" "" "$INSTDIR\ioquake3.x86.exe" 0

SectionEnd

Section "Protocol Handler" ProtocolHandler

  WriteRegStr SHCTX "Software\Classes\quake3" "CustomUrlApplication" "$INSTDIR\ioquake3.x86.exe"
  WriteRegStr SHCTX "Software\Classes\quake3" "CustomUrlArguments" '--uri "%1"'
  WriteRegStr SHCTX "Software\Classes\quake3" "URL Protocol" ""
  WriteRegStr SHCTX "Software\Classes\quake3\DefaultIcon" "" "$INSTDIR\ioquake3.x86.exe,0"
  WriteRegStr SHCTX "Software\Classes\quake3\shell\open\command" "" '"$INSTDIR\ioquake3.x86.exe" --uri "%1"'

SectionEnd

!ifdef USE_OPENAL_DLOPEN
Section "OpenAL-Soft library" OpenAL

  SetOutPath $INSTDIR

  File "OpenAL32.dll"

SectionEnd
!endif

!ifdef USE_CURL_DLOPEN
Section "libcurl" libcurl

  SetOutPath $INSTDIR

  File "libcurl-4.dll"

SectionEnd
!endif

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\ioquake3"
  DeleteRegKey SHCTX "Software\ioquake3"

  ReadRegStr $0 SHCTX "Software\Classes\quake3\shell\open\command" ""
  ${If} $0 == '"$INSTDIR\ioquake3.x86.exe" --uri "%1"'
    DeleteRegKey SHCTX "Software\Classes\quake3"
  ${EndIf}

  ; Remove files and uninstaller
  Delete $INSTDIR\baseq3\cgamex86.dll
  Delete $INSTDIR\baseq3\qagamex86.dll
  Delete $INSTDIR\baseq3\uix86.dll
  Delete $INSTDIR\missionpack\cgamex86.dll
  Delete $INSTDIR\missionpack\qagamex86.dll
  Delete $INSTDIR\missionpack\uix86.dll
  Delete $INSTDIR\ioquake3.x86.exe
  Delete $INSTDIR\ioq3ded.x86.exe
!ifdef USE_RENDERER_DLOPEN
  Delete $INSTDIR\renderer_opengl1_x86.dll
  Delete $INSTDIR\renderer_opengl2_x86.dll
!endif

  Delete $INSTDIR\COPYING.txt
  Delete $INSTDIR\README.txt
  Delete $INSTDIR\id-readme.txt
  Delete $INSTDIR\voip-readme.txt

  Delete $INSTDIR\SDL2.dll
!ifndef USE_INTERNAL_ZLIB
  Delete $INSTDIR\zlib1.dll
!endif
!ifndef USE_INTERNAL_JPEG
  Delete $INSTDIR\jpeg8c.dll
!endif

!ifdef USE_OPENAL_DLOPEN
  Delete $INSTDIR\OpenAL32.dll
!endif
!ifdef USE_CURL_DLOPEN
  Delete $INSTDIR\libcurl-4.dll
!endif

  Delete $INSTDIR\uninstall.exe

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\ioquake3\Uninstall.lnk"
  Delete "$SMPROGRAMS\ioquake3\ioquake3.lnk"

  ; Remove directories used
  RMDir "$SMPROGRAMS\ioquake3"
  RMDir "$INSTDIR\baseq3"
  RMDir "$INSTDIR\missionpack"
  RMDir "$INSTDIR"

SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${ioquake3} "The game executables."
  !insertmacro MUI_DESCRIPTION_TEXT ${StartMenuShortcuts} "Create shortcuts in the start menu."
  !insertmacro MUI_DESCRIPTION_TEXT ${ProtocolHandler} "The protocol handler lets you connect to a game by clicking a link in a web browser."
!ifdef USE_OPENAL_DLOPEN
  !insertmacro MUI_DESCRIPTION_TEXT ${OpenAL} "Advanced audio mixer that supports surround sound."
!endif
!ifdef USE_CURL_DLOPEN
  !insertmacro MUI_DESCRIPTION_TEXT ${libcurl} "Used for HTTP file downloads."
!endif
!insertmacro MUI_FUNCTION_DESCRIPTION_END
